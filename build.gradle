
buildscript {
    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.12.2'
    }
}


subprojects {
    repositories {
        mavenCentral()
        google()
    }

    plugins.withId('com.android.library') {
        android.lintOptions {
            textReport true
            textOutput 'stdout'
            lintConfig rootProject.file('lint.xml')

            checkDependencies true
            checkTestSources false // TODO true https://issuetracker.google.com/issues/138247523
            explainIssues false

            // We run a full lint analysis as build part in CI, so skip vital checks for assemble task.
            checkReleaseBuilds false
        }
    }


    tasks.withType(Test).configureEach {
        testLogging {
            if (System.getenv('CI') == 'true') {
                events = ['failed', 'skipped', 'passed']
            }
            exceptionFormat 'full'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.add('-Xlint:-options')
    }
}
